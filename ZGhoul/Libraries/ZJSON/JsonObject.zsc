
class GFJsonObjectKeys
{
	Array<String> keys;
}

class GFJsonObject : GFJsonElement
{
	Map<String,GFJsonElement> data;
	
	static GFJsonObject make()
	{
		return new("GFJsonObject");
	}
	// [MC] Added option to disable case sensitivity.
	GFJsonElement Get(String key, bool sensitive = true)
	{
		if (!sensitive)
		{
			key.MakeLower();
			foreach (k, v : data)
			{
				k.MakeLower();
				if (key == k)
					return v;
			}
			return null;
		}
		if(!data.CheckKey(key)) return null;
		return data.Get(key);
	}
	
	void Set(String key,GFJsonElement e)
	{
		data.Insert(key,e);
	}
	
	bool Insert(String key, GFJsonElement e)
	{ // only inserts if key doesn't exist, otherwise fails and returns false
		if(data.CheckKey(key)) return false;
		data.Insert(key,e);
		return true;
	}
	
	bool Delete(String key)
	{
		if(!data.CheckKey(key)) return false;
		data.Remove(key);
		return true;
	}
    
	void GetKeysInto(out Array<String> keys)
	{
		keys.Clear();
		MapIterator<String,GFJsonElement> it;
		it.Init(data);
		while(it.Next()){
			keys.Push(it.GetKey());
		}
	}
    
	GFJsonObjectKeys GetKeys()
	{
		GFJsonObjectKeys keys = new("GFJsonObjectKeys");
        GetKeysInto(keys.keys);
		return keys;
	}
    
    deprecated("0.0", "Use IsEmpty Instead") bool Empty()
	{
        return data.CountUsed() == 0;
    }

	bool IsEmpty()
	{
		return data.CountUsed() == 0;
	}
	
	void Clear()
	{
		data.Clear();
	}
	
	uint Size()
	{
		return data.CountUsed();
	}
	
	override string Serialize()
	{
		String s;
		s.AppendCharacter("{");
		bool first = true;
		
		MapIterator<String,GFJsonElement> it;
		it.Init(data);
		
		while(it.Next()){
			if(!first){
				s.AppendCharacter(",");
			}
			s.AppendFormat("%s:%s", GFJSON.serialize_string(it.GetKey()), it.GetValue().serialize());
			first = false;
		}
		
		s.AppendCharacter("}");
		return s;
	}
    
	override string GetPrettyName()
	{
		return "Object";
	}
}
